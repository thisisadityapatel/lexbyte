options {
  IGNORE_CASE=false;
  MULTI=true;	// This will generate one AST class for each non-suppressed non-terminal
  JJTREE_OUTPUT_DIRECTORY="AST";  // This will put all your AST classes in the AST directory
  VISITOR=true;	// This won't be used until the next assignment, but will be needed to make your assignment compile properly
  VISITOR_EXCEPTION="Exception";
}

PARSER_BEGIN(VNM)

public class VNM {
}

PARSER_END(VNM)

TOKEN_MGR_DECLS : 
{

}

SKIP : {
  " "     
  |   "\t"    
  |   "\n"    
  |   "\r" 
}

TOKEN : 
{
  < LESS: "<">
| < LE:     "<="  >
| < GREAT:  ">"   >
| < GE:     ">="  >
| < EQ:     "=="  >
| < NEQ:    "!="  >
| < IN:     "=in" >
| < NOTIN:  "!in" >
| < PLUS:	  "+"   >
| < MINUS:	"-"   >
| < TIMES:	"*"   >
| < DIV:	  "/"   >
| < AND:	  "&"   >
| < VBAR:	  "|"   >
| < NOT:	  "!"   >
| < ASSGN:	":="   >
| < LRND:	  "("   >
| < LSQU:	  "["   >
| < RRND:	  ")"   >
| < RSQU:	  "]"   >
| < COMMA:	","   >
| < SCOL:	  ";"   >
| < TRUE:	  "#1"  >
| < FALSE:	"#0"  >
| < RANGE:	".."  >
}

TOKEN [IGNORE_CASE]:
{
  < DO:"DO">
| < WHILE:"WHILE">
| < FOR:"FOR">
| < IF:"IF">
| < THEN:"THEN">
| < ELIF:"ELIF">
| < ELSE:"ELSE">
| < FI:"FI">
| < FUNCTION:"FUNCTION">
| < RETURN:"RETURN">
| < END:"END">
| < PRINT:"PRINT">
| < PRINTLN:"PRINTLN">
| < VAR:"VAR">
}

TOKEN : 
{
  < #DIGIT:	["0"-"9"]>
|	< #LOWER:	["a"-"z"]>
|	< #UPPER:	["A"-"Z"]>
|	< #LETTER: ["a"-"z","A"-"Z"]>

// Passes strings1 and strings2.
| < STRING: "\"" (~["\""])* "\"" > 
|	< NUMBER:	(<DIGIT>)+ >
|	< IDNUM:  "#" <LETTER>(<LETTER>|<DIGIT>)* >  
|	< IDBOOL:	"?" <LETTER>(<LETTER>|<DIGIT>)* >
|	< IDVEC:  "v_" (<LETTER>|<DIGIT>)+ >  

}

// Special tokens are saved, but not sent to the parser
SPECIAL_TOKEN : 
{
  <COMMENT_SINGLE: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}



// ==================================================
//                  PARSER BEGINS
// ==================================================

SimpleNode start	() #void :
{}
{  S()  { return (SimpleNode) (jjtree.popNode()); }
| < EOF > {throw new ParseException("End of File.");}
}

// entry point to parser. Called by TestVNM.java
void S() throws ParseException	#void	 :
{}
{	statement_LL1() ";"
| LOOKAHEAD(identifier() ":=") assign_stat() ";" 
| expression() ";" | boolean_call() ";"
}

//------------------------    STATEMENTS, BODIES AND CLAUSES -----------------------------------

// These are all the statements which can be differentiated from each other 
// with a single lookahead
void statement_LL1()	#void	 :
{}
{	var_decl()
| fn_decl()
| return_stat()
| print_stat()
| println_stat()
| if_stat()
| for_stat()
| while_stat()
}

// These are the statements not at the top level, i.e. inside bodies or clauses
// Maintain structural integrity of assignments and function calls by not factoring them
// and using a lookahead to distinguish between them

void statement()	#void :
{}
{	statement_LL1()
| LOOKAHEAD(2) fn_call()
| assign_stat()
}

void body()		 :
{}
{	(statement() ";")*
}

void clause()		 :
{}
{	(statement() ";")+
}

//---------------------------   DECLARATIONS ------------------------------------------------

void var_decl()	#void :
{}
{	<VAR> var_list()
}

void var_list()	#void :
{}
{	(identifier() ("," identifier())*) #var_decl
}

void fn_decl()		 :
{}
{	<FUNCTION> identifier() "(" (ident_list())? ")" body() <END>
}

void ident_list()	 :
{}
{	identifier() ("," identifier())*
}


//---------------------------   FUNCTION CALLS AND RETURNS ----------------------------------

// Separate out function calls that can be used in expressions (numbers and vectors),
//     from those used in conditions (boolean).
// While this simplifies some things, it adds ugliness elsewhere, e.g. boolean_call() now must be
//    explicitly looked for in start()... tradeoffs... 
void fn_call()		 :
{}
{	idnum() "(" (exp_list())? ")"
|	idvec() "(" (exp_list())? ")"
}

void boolean_call()		 :
{}
{	idbool() "(" (exp_list())? ")"
}

void exp_list()	 :
{}
{	expression() ("," expression())*
}

// Since we split function calls by boolean and numerical,
//    we need an explicit condition list
void condition_list() :
{}
{ condition() ("," condition())*
}

void return_stat()	#Return :
{}
{	<RETURN> returnval()
}

// syntactic lookahead to distinguish conditions and expressions.
//    this is needed since a condition() can begin with an expression()
void returnval()	#void :
{}
{	LOOKAHEAD(condition()) condition()
| expression() 
}

void print_stat() #Print :
{}
{	<PRINT> print_list()
}

void println_stat() #Print_ln :
{}
{ <PRINTLN> (print_list())?
}

void print_list()	#void :
{}
{	
  (expression() | string() | idbool()) ("," (expression() | string() | idbool()))*
}

void assign_stat() #Assign :
{}
{ idnum() ":=" expression()
| idbool() ":=" condition()
| idvec() ":=" vec_const()
}

void if_stat()		#If :
{}
{	<IF> condition() <THEN> clause() else_clause() <FI>
}

void else_clause	() #void:
{}
{	 (<ELIF> condition() <THEN> clause() else_clause()) #If
|  <ELSE> clause()
|  {} #NULL
}

void for_stat()	#For :
{}
{	<FOR> idnum() <IN> exp_list() <DO> body() <END>
}

void while_stat() #While	 :
{}
{	<WHILE> condition() <DO> body() <END>
}


//---------------------------   CONDITIONS ---------------------------------------------------

void condition()	#void :
{}
{	(and_clause() ("|" and_clause())*) #or(>1)
}

void and_clause()	#void :
{}
{	(not_clause() ("&" not_clause())*) #and(>1)
}

// syntactic lookahead to distinguish comparisons and conditions.
// use boolean_call() instead of fn_call
void not_clause()	#void :
{}
{	"!" not_clause()  #not
| LOOKAHEAD(expression() comparator()) comparison() | "(" condition() ")"
| LOOKAHEAD(2) boolean_call()
| bool_simple()
}

void comparison()	 :
{}
{	expression() comparator() expression()
}

void comparator()	 #void :
{}
{	"<"  #le
| "<="  #leq
| ">"  #gre
| ">="  #geq
|"=="   #eq
| "!="   #neq
| "=in"   #in
| "!in"   #notin
}


//---------------------------   EXPRESSIONS ------------------------------------------------

void expression()	#void	 :
{}
{	("+" product() (summand())*) #sum(>1)
|	(neg() (summand())*) #sum
|	(product() (summand())*)  #sum(>1)
}

void neg() :
{}
{	"-" product() 
}

 void summand() #void :
{}
{	"+" product() #pos
|	"-" product() #neg
}
 
void product() #void :
{}
{	term() moreterms()
}

void moreterms() #void :
{}
{("*" term()) #mul(2) moreterms()
|("/" term()) #div(2) moreterms()
|("%" term()) #mod(2) moreterms()
| {}
}

// void expression() #void:
// {}
// {	("+" | "-")? product() (("+" | "-") product())*
// }

// void product()		 :
// {}
// {	term() (("*" | "/") term())*
// }

// use a lookahead to distinguish fn_call from simple_term.
//    simple_term and fn_calls are quite distinct so factoring would be confusing
void term()		#void :
{}
{	"(" expression() ")"
| LOOKAHEAD(2) fn_call()
| simple_term()
}

void simple_term()	#void :
{}
{	idnum()
| idvec()
| number()
| vec_const() 
}

// Again, with splitting out expression and condition productions,
//    in order to have vectors of booleans, we need explicitly add them
//    and we need the syntactic lookahead to distinguish between them
//    (recall that conditions can begin with an expression)
void vec_const()	 :
{}
{	
  LOOKAHEAD("[" expression()) "[" (exp_list())? "]"
| "[" (condition_list())? "]"
}

void identifier()	#void :
{}
{	idnum()
| idbool()
| idvec() 
}

void bool_simple()	#void	 :
{}
{	idbool() 
| <TRUE>  #TRUE
| <FALSE>  #FALSE
}   


void idvec ()  :
{Token t;}
{  (t=<IDVEC>)  {jjtThis.jjtSetValue(t.getValue());}
}

void idnum () :
{Token t;}
{  (t=<IDNUM>) {jjtThis.jjtSetValue(t.getValue());}
}  

void idbool ()  :
{Token t;}
{  (t=<IDBOOL>)  {jjtThis.jjtSetValue(t.getValue());}
}

void number () :
{Token t;}
{  t=<NUMBER>  {jjtThis.jjtSetValue(t.getValue());}
}

void string () :
{Token t;}
{  t=<STRING>  {jjtThis.jjtSetValue(t.getValue());}
}
   